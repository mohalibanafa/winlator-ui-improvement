apply plugin: 'com.android.application'

android {
    compileSdk 30

    def storeFileProp = System.getenv("SIGNING_STORE_FILE") ?: project.findProperty("SIGNING_STORE_FILE")
    def storePasswordProp = System.getenv("SIGNING_STORE_PASSWORD") ?: project.findProperty("SIGNING_STORE_PASSWORD")
    def keyAliasProp = System.getenv("SIGNING_KEY_ALIAS") ?: project.findProperty("SIGNING_KEY_ALIAS")
    def keyPasswordProp = System.getenv("SIGNING_KEY_PASSWORD") ?: project.findProperty("SIGNING_KEY_PASSWORD")
    def hasReleaseSigning = storeFileProp != null && storePasswordProp != null && keyAliasProp != null && keyPasswordProp != null

    signingConfigs {
        if (hasReleaseSigning) {
            release {
                storeFile file(storeFileProp)
                storePassword storePasswordProp
                keyAlias keyAliasProp
                keyPassword keyPasswordProp
            }
        }
    }

    defaultConfig {
        applicationId 'com.winlator'
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 16
        versionName "7.1"
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig hasReleaseSigning ? signingConfigs.release : signingConfigs.debug
            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    ndkVersion '22.1.7171670'

    externalNativeBuild {
        cmake {
            version '3.22.1'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.github.luben:zstd-jni:1.5.2-3@aar'
    implementation 'org.tukaani:xz:1.7'
    implementation 'org.apache.commons:commons-compress:1.20'
}
